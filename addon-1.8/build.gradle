buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }

        dependencies {
            classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
            classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        }
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle.forge'

compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly files('../libs/lm_api_mc1.8.9.jar')
    compileOnly project(":addon-core")
    compileOnly("org.jetbrains:annotations:21.0.1")
}

compileJava.doFirst {
    copy {
        from './../addon-core/src/main/java'
        into './build/sources/main/java'
    }
    copy {
        from './src/main/java'
        into './build/sources/main/java'
    }
}

minecraft {
    version = "1.8.9-11.15.1.1855"
    runDir = "run"
    mappings = "stable_20"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    from './../addon-core/src/main/resources'
}

shadowJar {
    configurations = [project.configurations.compile]
    archiveName 'LabyTwitch.jar'

    exclude 'net/labymod/**'
    exclude 'net/jpountz/**'
    exclude 'assets/minecraft/labymod/**'
    exclude 'BytecodeMethods.class'
    exclude 'dummyThing'
    exclude 'mcmod.info'
}

reobf {
    shadowJar {
        setMappingType(net.minecraftforge.gradle.user.ReobfMappingType.NOTCH)
    }
}